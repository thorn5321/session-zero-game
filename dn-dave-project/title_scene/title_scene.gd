extends Node2D

const START_LEVEL : String = "res://Levels/GameDayOne/InnInterior.tscn"

@export var music : AudioStream 
@export var button_focus_audio : AudioStream 
@export var button_press_audio : AudioStream 

@onready var button_new: Button = $CanvasLayer/Control/ButtonNew
@onready var button_continue: Button = $CanvasLayer/Control/ButtonContinue
@onready var button_quit: Button = $CanvasLayer/Control/ButtonQuit
@onready var audio_stream_player: AudioStreamPlayer = $AudioStreamPlayer


func _ready() -> void:
	get_tree().paused = true
	PlayerManager.player.visible = false
	##PlayerHud.visible = false
	PauseMenu.process_mode = Node.PROCESS_MODE_DISABLED
	
	if SaveManager.get_save_file() == null:
		button_continue.disabled = true
		button_continue.visible = false
	
	setup_title_screen()
	
	LevelManager.level_load_started.connect(exit_title_screen)


func setup_title_screen() -> void: 
	AudioManager.play_music(music)
	
	button_new.pressed.connect(start_game)
	button_continue.pressed.connect(load_game)
	button_quit.pressed.connect(quit_game)
	
	button_new.grab_focus()
	
	button_new.focus_entered.connect(play_audio.bind(button_focus_audio))
	button_continue.focus_entered.connect(play_audio.bind(button_focus_audio))
	button_quit.focus_entered.connect(play_audio.bind(button_focus_audio))


func start_game() -> void:
	play_audio(button_press_audio)

	LevelManager.change_tilemap_bounds([
		Vector2(-10000000, -10000000),
		Vector2(10000000, 10000000)
	])

	await LevelManager.load_new_level(
		START_LEVEL,
		"NewGameStart",
		Vector2.ZERO
	)

func load_game() -> void:
	play_audio(button_press_audio)

	LevelManager.change_tilemap_bounds([
		Vector2(-10000000, -10000000),
		Vector2(10000000, 10000000)
	])

	SaveManager.load_game()

func quit_game() -> void: 
	get_tree().quit()
	

func exit_title_screen() -> void:
	PlayerManager.player.visible = true
	##PlayerHud.visible = true
	PauseMenu.process_mode = Node.PROCESS_MODE_ALWAYS
	queue_free()


func play_audio(_a : AudioStream) -> void:
	audio_stream_player.stream = _a
	audio_stream_player.play()
